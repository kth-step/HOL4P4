embed
{{ hol
open bitstringTheory;
open wordsTheory;
open alistTheory;
}}

%%%%%%%%%%%%%%%%%%%
% Primitive types %
%%%%%%%%%%%%%%%%%%%

%x denotes variable name, f denotes field name or function name, tbl denotes table name
metavar x, f, tbl ::=
      {{ lex alphanum }}
      {{ hol string }}
      {{ tex [[x]] }}
      {{ com string }}

%TODO Numeral or alphanum?
metavar b ::=
      {{ lex numeral }}
      {{ hol bool }}
      {{ tex [[b]] }}
      {{ com boolean }}

metavar bl ::=
      {{ lex numeral }}
      {{ hol bool list }}
      {{ tex [[bl]] }}
      {{ com bit-string }}


%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Auxiliary metavariables %
%%%%%%%%%%%%%%%%%%%%%%%%%%%

%w represents the width of a bitstring
metavar i, w ::=
      {{ lex numeral }}
      {{ hol num }}
      {{ tex [[i]] }}
      {{ com natural number }}

indexvar m, n, o ::=
  {{ lex numeral }}
  {{ coq nat }}
  {{ hol num }}
  {{ com indices }}


grammar

%%%%%%%%%%%%%%%%%%%%
% Auxiliary syntax %
%%%%%%%%%%%%%%%%%%%%

%This just exists in order to be able to use the below productions in the semantics
num_exp :: num_exp ::=
{{ hol num }}
{{ com expressions with numerals }}
| 1 :: M :: one
  {{ hol 1 }}
| i :: M :: i
  {{ hol [[i]] }}
| num_exp + num_exp' :: M :: add
  {{ hol ( [[num_exp]] + [[num_exp']] ) }}
| num_exp - num_exp' :: M :: sub
  {{ hol ( [[num_exp]] - [[num_exp']] ) }}
| ( num_exp ) :: M :: par
  {{ hol ( [[num_exp]] ) }} 

%Note: Headers denote only the start of the syntax for their respective components.
%      Some parts may be defined later under other headers, for technical reasons.
%%%%%%%%%%
% Values %
%%%%%%%%%%

boolv :: boolv_ ::=
{{ hol bool }}
{{ com boolean }}
| true :: M :: true
  {{ hol T }}
  {{ tex { \top } }}
| false :: M :: false
  {{ hol F }}
  {{ tex { \bot } }}
| b :: M :: free
  {{ hol [[b]] }}

bitv :: bitv_ ::=
{{ hol (bl # num) }}
{{ com bit-string }}
| bl n :: M :: bl
  {{ hol ([[bl]], [[n]]) }}

%Fence so that ott does not put packet_in before boolv
embed
{{ hol

}}
grammar

v :: v_ ::=
{{ com value }}
| boolv :: :: bool
  {{ com boolean value }}
| bitv :: :: bit
  {{ com bit-string }}
| x :: :: str
  {{ com string literal }} 
| struct { x1 = v1 ; ... ; xn = vn } :: :: struct
  {{ com struct }}
| header boolv { x1 = v1 ; ... ; xn = vn } :: :: header
  {{ com header }}
%Error messages are now serialised as 32-bit 0, 1, 2, ... based on order of declaration.
%| errmsg x :: :: err
%  {{ com error message }}
| ext_ref i :: :: ext_ref
  {{ com extern object reference }}
| bot :: :: bot
  {{ com no value }}

v_list :: v_list_ ::=
{{ com list of values }}
{{ hol (v list) }}
| empty_vl :: M :: empty
  {{ com empty expression list }}
  {{ hol ([]:v list) }}
| v1 , .. , vn :: M :: vl
  {{ hol ([[v1 .. vn]]) }}

%An abstract function or extern method/function name
funn :: funn_ ::=
| f :: :: name
  {{ com function name }}
| inst f :: :: inst
  {{ com extern object instantiation }}
| f f' :: :: ext
  {{ com extern method call }}
| ( funn ) :: X :: paren
  {{ hol ([[funn]]) }}

%An abstract variable name
varn :: varn_ ::=
| x :: :: name
  {{ com variable name }}
| ( star , funn ) :: :: star 
  {{ com function return placeholder }}
  {{ tex  \ast ^ [[funn]]  }}


%%%%%%%%%%%%%%%%%%%
% Execution state %
%%%%%%%%%%%%%%%%%%%
%Note that the execution state is not part of the P4 specification, but made up for verification purposes.

%Execution status: used to model function return and parser state transitions
status {{ tex t }}:: status_ ::=
{{ com execution status }}
| Running :: I :: running
  {{ tex { \mathbf{run} } }}
| Ret v :: I :: returnv
  {{ tex { \mathbf{ret} \; [[v]] } }}  
| Trans x :: I :: trans
  {{ com transition to parser state }}
  {{ tex { \mathbf{tra} \; [[x]] } }}

mk {{ tex \mathit{mk} }} :: mk_ ::=
{{ com matching kinds }}
| exact :: :: exact
  {{ tex \mathrm{exact} }}
| ternary :: :: ternary
  {{ tex \mathrm{ternary} }}
| lpm :: :: lpm
  {{ tex \mathrm{lpm} }}

%Fence between mk and mk_list
embed
{{ hol

}}
grammar

mk_list {{ tex \mathit{mk\_list} }} :: mk_list_ ::=
{{ com matching kinds list }}
{{ hol (mk list) }}
| empty :: :: empty
  {{ com empty match kind list }}
  {{ hol ([]:mk list) }}
| [ mk1 , .. , mkn ] :: M :: list
  {{ com list of match kinds }}
  {{ hol ([[mk1 .. mkn]]) }}
| ( mk_list ) :: S :: paren
  {{ hol ([[mk_list]]) }}


%%%%%%%%%%%%%%%
% Expressions %
%%%%%%%%%%%%%%%

unop {{ tex \ominus }}:: unop_ ::=
| ! :: :: neg
  {{ com negation }}
  {{ tex ! }}
| ~ :: :: compl
  {{ com bitwise complement }}
  {{ tex \neg }}
| - :: :: neg_signed
  {{ com signed negation }}
| + :: :: un_plus
  {{ com unary plus }}

binop {{ tex \oplus }} :: binop_ ::=
| * :: :: mul
  {{ com multiplication }}
  {{ tex \times }}
| / :: :: div
  {{ com division }}
| mod :: :: mod
  %Denoted by % in P4 spec
  {{ com modulo }}
  {{ tex \bmod }}
| + :: :: add
  {{ com addition }}
| - :: :: sub
  {{ com subtraction }}
| SHL :: :: shl
  {{ com logical left-shift }}
  {{ tex \ll }}
| SHR :: :: shr
  {{ com logical right-shift }}
  {{ tex \gg }}
| LE :: :: le
  {{ com less or equal }}
  {{ tex \leq }}
| GE :: :: ge
  {{ com greater or equal }}
  {{ tex \geq }}
| < :: :: lt
  {{ com less }}
  {{ tex < }}
| > :: :: gt
  {{ com greater }}
  {{ tex > }}
| NE :: :: neq
  {{ com not equal }}
  {{ tex \neq }}
| EQ :: :: eq
  {{ com equal }}
  {{ tex = }}
| & :: :: and
  {{ com bitwise and }}
| ^ :: :: xor
  {{ com bitwise xor }}
  {{ tex \underline{\vee} }}
| '|' :: :: or
  {{ com bitwise or }}
  {{ tex \mid }}
| AND :: :: bin_and
  {{ com binary and }}
  {{ tex \land }}
| OR :: :: bin_or
  {{ com binary or }}
  {{ tex \lor }}

e :: e_ ::=
{{ com expression }}
| v :: :: v
  {{ com value }}
| var varn :: :: var
  {{ com variable }}
| { e1 , .. , en } :: :: list
  {{ com expression list }}
| e . x :: :: acc
  {{ com field access }}
| unop e :: :: unop
  {{ com unary operation }}
  {{ tex [[unop]] [[e]] }}
| e1 binop e2 :: :: binop
  {{ com binary operation }}
  {{ tex [[e1]] [[binop]] [[e2]] }}
| concat e1 e2 :: :: concat
  {{ com concatenation of bit-strings }}
| e1 [ e2 : e3 ] :: :: slice
  {{ com bit-slice }}
| call funn ( e1 , .. , en ) :: :: call
  {{ com function or extern call }}
| select e { v1 : x1 ; ... ; vn : xn } x :: :: select
  {{ com select }}
| eStruct { x1 = e1 ; ... ; xn = en } :: :: struct
  {{ com struct }}
| eHeader boolv { x1 = e1 ; ... ; xn = en } :: :: header
  {{ com header }}  
| ( e ) :: S :: paren
  {{ hol ([[e]]) }}

embed
{{ hol

}}
grammar

e_list :: e_list_ ::=
{{ com list of expressions }}
{{ hol (e list) }}
| empty_el :: M :: empty
  {{ com empty expression list }}
  {{ hol ([]:e list) }}
| e1 , .. , en :: M :: exps
  {{ com list of expressions }}
  {{ hol ([[e1 .. en]]) }}
| e '::' e_list :: M :: cons
  {{ com cons }}
  {{ hol ([[e]]::[[e_list]]) }}
| tl e_list :: M :: tail
  {{ com tail }}
  {{ hol (TL [[e_list]]) }}
| [ e1 , .. , en ] :: M :: list
  {{ com bracketed list of expressions }}
  {{ hol ([[e1 .. en]]) }}
| e_list ++ e_list' :: M :: append
  {{ com append two expression lists }}
  {{ hol ([[e_list]] ++ [[e_list']]) }}
| update ( e , i , e_list ) :: M :: update
  {{ com update entry of expression list }}
  {{ hol (LUPDATE [[e]] [[i]] [[e_list]]) }}
  {{ tex ([[i]] \mapsto  [[e]]) [[e_list]] }}
| ( e_list ) :: S :: paren
  {{ hol ([[e_list]]) }}


lval :: lval_ ::=
| varn :: :: varname
  {{ com variable name }}
| null :: :: null
  {{ com null variable }} 
| lval . f :: :: field
  {{ com field access }}
| lval [ e1 : e2 ]  :: :: slice
  {{ com slice array }}  
| ( lval ) :: X :: paren
  {{ hol ([[lval]]) }}


embed
{{ hol

}}
grammar

%This is the structure that maps variable names to values (and with copy-out variables)
scope {{ tex \gamma }} :: scope_ ::=
{{ hol ((varn, (v # lval option)) alist) }}
{{ com variable environment of current scope }}
| empty :: M :: empty
  {{ com empty scope }}
  {{ hol [] }}
  {{ tex {\gamma}_{\emptyset} }}
| ( scope ) :: S :: paren
  {{ hol ([[scope]]) }}
  {{ tex  [[scope]] }}

%A global scope is of the same type as a regular scope, but is used and typeset differently
g_scope {{ tex \gamma_{ \mathrm{G} } }} :: g_scope_ ::=
{{ com global scope }}
{{ hol scope }}
| scope :: M :: global
  {{ hol ([[scope]]) }}



%Fence between gscope and g_scope_list
embed
{{ hol

}}
grammar

%The global scope list holds the global scope as well as the block-global scope
g_scope_list {{ tex \overline{\gamma_G} }} :: g_scope_list_ ::=
{{ hol (scope list) }}
{{ com list of the global scope and the block-global scope }}
| [ g_scope ]' :: M :: one
  {{ com only global scope }}
  {{ hol ([ [[g_scope]] ]) }}
  {{ tex [ [[g_scope]] ] }}
| [ g_scope ; g_scope' ] :: M :: two
  {{ com global scope and block-global scope }}
  {{ hol ([ [[g_scope]] ; [[g_scope']] ]) }}
  {{ tex [ [[g_scope]], [[g_scope']] ] }}
| take 1 g_scope_list :: M :: take
  {{ com take first element }}
  {{ hol (TAKE 1 [[g_scope_list]]) }}
  {{ tex \mathrm{take}(1,[[g_scope_list]]) }}
| lastn num_exp g_scope_list :: M :: lastn
  {{ com last n elements }}
  {{ hol (LASTN [[num_exp]] [[g_scope_list]]) }}
  {{ tex \mathrm{lastn}([[num_exp]],[[g_scope_list]]) }}

scope_list {{ tex \overline{\gamma} }} :: scope_list_ ::=
{{ hol (scope list) }}
{{ com stack of block scopes }}
| empty :: M :: empty
  {{ hol ([]:scope list) }}
  {{ tex {[\;]} }}
| [ scope1 , .. , scopen ] :: M :: list
  {{ hol ([[scope1 .. scopen]]) }}
| tl scope_list :: M :: tl
  {{ hol (TL [[scope_list]]) }}
  {{ tex \mathrm{tl} \; [[scope_list]] }}
| g_scope '::' scope_list' :: M :: cons
  {{ hol ([[g_scope]] :: [[scope_list']]) }}
  {{ tex [[g_scope]] :: [[scope_list']] }}
| scope_list ++ scope_list' :: M :: append
  {{ hol ([[scope_list]] ++ [[scope_list']]) }}
  {{ tex [[scope_list]] \mathrel{\mathop+}+ [[scope_list']] }}
| rev scope_list :: M :: rev
  {{ hol (REVERSE [[scope_list]]) }}
  {{ tex \mathrm{rev} ([[scope_list]]) }}
| update ( scope , i , scope_list ) :: M :: update_scope
  {{ com update a scope in the stack frame }}
  {{ hol ( LUPDATE [[scope]] [[i]] [[scope_list]] ) }}
  {{ tex ([[i]] \mapsto [[scope]]) [[scope_list]] }}
| g_scope_list :: M :: g_scope_list
  {{ com g_scope_list }}
  {{ hol ( [[g_scope_list]] ) }}
  {{ tex [[g_scope_list]] }}
| ( scope_list ) :: S :: paren
  {{ hol ([[scope_list]]) }}

%An extern function/method updates the scope list, global scope list and arch scope directly.
%Note that at the time this happens, a new frame has already been declared for it to work inside.
ext_fun {{ tex { \mathit{ext\_fun} } }} :: ext_fun_ ::=
{{ hol ((ascope_ty # g_scope_list # scope_list) -> ((ascope_ty # scope_list # status) option)) }}

%%%%%%%%%%%
% Context %
%%%%%%%%%%%

%Note that since the semantics is modular with respect to the architecture, A is typeset in cursive as a variable here
%Note that a Python script later rewrites the export and add the proper polymorphic type in place of "ascope_ty"
ascope {{ tex { {\gamma}_A } }}, ascope_ty :: ascope_ ::=
{{ com architectural scope }}
{{ hol ('a) }}

d {{ tex d }} :: d_ ::=
{{ com parameter direction }}
| 'in :: :: in
  {{ tex \downarrow }}
| out :: :: out
  {{ tex \uparrow }}
| inout :: :: inout
  {{ tex \updownarrow }}
| none :: :: none
  {{ tex \circ }}

%The "fixed function" of a fixed-function block
ff {{ tex { \mathit{ff} } }} :: ff_ ::=
{{ hol (ascope_ty -> ascope_ty option) }}

d_list :: d_list_ ::=
{{ com list of directions }}
{{ hol (d list) }}
| empty :: M :: empty
  {{ com empty direction list }}
  {{ hol ([]:d list) }}
| [ d1 , .. , dn ] :: M :: list
  {{ com list of directions }}
  {{ hol ([[d1 .. dn]]) }}
| d_list ++ d_list' :: M :: append
  {{ com append two e_lists }}
  {{ hol ([[d_list]] ++ [[d_list']]) }}
| ( d_list ) :: S :: paren
  {{ hol ([[d_list]]) }}


%%%%%%%%%%%%%
%   Types   %
%%%%%%%%%%%%%

x_list :: x_list_ ::=
{{ com list of strings }}
{{ hol (x list) }}
| empty :: M :: empty
  {{ com empty list }}
  {{ hol ([]:x list) }}
| [ x1 , .. , xn ] :: M :: list
  {{ com list of strings }}
  {{ hol ([[x1 .. xn]]) }}
| x_list ++ x_list' :: M :: append
  {{ com append lists }}
  {{ hol ([[x_list]] ++ [[x_list']]) }}
| ( x_list ) :: S :: paren
  {{ hol ([[x_list]]) }}

struct_ty :: struct_ty_ ::=
| struct :: :: struct
| header :: :: header

tau {{ tex \tau }} :: tau_ ::=
{{ com type }}
| bool :: :: bool
  {{ com boolean }}
| bs num_exp :: :: bit
  {{ com bit-string }}
  {{ tex { { \mathbf{bs} }^{[[num_exp]]} } }}   
| bot :: :: bot
  {{ com no value }}
  {{ tex \bot}}
| struct_ty [ x1 tau1 , .. , xn taun ] :: :: xtl
  {{ com struct }}
%TODO: Why is the string literal type a list?
| x_list :: :: xl   
  {{ com string literal }}
| ext :: :: ext
  {{ com extern }}



t {{ tex t }} :: t_ ::=
{{ com type }}
| tau :: :: tau
  {{ com BASE TYPE }}
| x_list :: :: string_names_a   
  {{ com represents parser states' names ex. a , not literals "a" }} 



taul :: taul_ ::=
{{ hol (tau list) }}
| empty :: M :: empty
  {{ hol ([]:tau list) }}
| [ tau1 , .. , taun ] :: M :: list
  {{ hol ([[tau1 .. taun]]) }}  

Ftau :: Ftau ::=
{{ com function names types }}
{{ hol ( ( tau # x # d ) list # tau) }}
| [ tau1 x1 d1 , .. , taun xn dn ] tau :: M :: list
  {{ hol ([[tau1 x1 d1 .. taun xn dn]] [[tau]]) }}


embed
{{ hol

}}
grammar


t_scope {{ tex \psi }} :: t_scope_ ::=
{{ hol ((varn, ( tau # lval option)) alist) }}
{{ com typing scope }}
| empty :: M :: empty
  {{ com empty typing scope }}
  {{ hol [] }}
  {{ tex {\psi}_{ \emptyset } }}
| ( t_scope ) :: S :: paren
  {{ hol ([[t_scope]]) }}
  {{ tex  [[t_scope]] }}


%Fence between types and statement
embed
{{ hol

}}
grammar


%%%%%%%%%%%%%%
% Statements %
%%%%%%%%%%%%%%

stmt :: stmt_ ::=
{{ com statement }}
| empty_stmt :: :: empty
  {{ com empty statement }}
  {{ tex \emptyset_{ \mathrm{stmt} } }}
| assign lval e :: :: ass
  {{ com assignment }}
  {{ tex [[lval]] \mathrel{\mathop:}= [[e]] }}
| if e then stmt1 else stmt2 :: :: cond
  {{ com conditional }}
| begin t_scope stmt end :: :: block
  {{ com block }}
  {{ tex \{ [[t_scope]] \, [[stmt]] \} }}
| return e :: :: ret
  {{ com return }}
| stmt1 ; stmt2 :: :: seq
  {{ com sequence }}
| verify e e' :: :: verify
  %e is the predicate, e' is the (serialised) error message
  {{ com verify }}
| transition e :: :: trans
  {{ com transition }}
  {{ tex \mathbf{transition}\, [[e]] }}
| apply tbl ( e1 , .. , en ) :: :: app
  {{ com apply }}
 % {{ tex \mathbf{apply} \, [[tbl]] \, [[e_list]] }}
| ext :: :: ext
  {{ com extern }}
  {{ tex \blacksquare  }}

%Fence between stmt and func_map
embed
{{ hol

}}
grammar

func_map {{ tex F_g }} :: func_map_ ::=
{{ com function map for global functions and actions }}
{{ hol ((string, (stmt # (string # d) list)) alist) }}

b_func_map {{ tex F_b }} :: b_func_map_ ::=
{{ com function map for block-local actions }}
{{ hol ((string, (stmt # (string # d) list)) alist) }}

%This maps extern function names of an individual object obj to their implementations
% method signature sig is = ((string # d) list # ext_fun) 

ext_fun_map {{ tex X_{ \mathrm{obj} } }} :: ext_fun_map_ ::=
{{ com extern function map }}
{{ hol ((string, ((string # d) list # ext_fun)) alist) }}

%Fence between ext_fun_map and ext_map
embed
{{ hol

}}
grammar

%This maps extern object names to tuples of optional constructors and the function map of the object in question
ext_map {{ tex X }} :: ext_map_ ::=
{{ com extern object map }}
{{ hol ((string, ((((string # d) list # ext_fun) option) # ext_fun_map)) alist) }}

tbl_map {{ tex { \mathit{Tb} } }} :: tbl_map_ ::=
{{ com table map }}
{{ hol ((string, ((mk list) # (x # e_list))) alist) }}
| empty :: :: empty
  {{ com empty table map }}
  {{ hol ([]:((string, ((mk list) # (x # e_list))) alist)) }}
| ( tbl_map ) :: S :: paren
  {{ hol ([[tbl_map]]) }}


pars_map {{ tex P }} :: pars_map_ ::=
{{ com parser state map }}
{{ hol ((string, stmt) alist) }}

in_out :: in_out_ ::=
{{ com input and output }}
{{ hol (bl # num) }}
| packet bl i :: M :: packet
  {{ hol ( [[bl]] , [[i]] ) }}

pbl_type :: pbl_type_ ::=
{{ com programmable block type }}
| parser :: :: parser
| control :: :: control

pblock {{ tex { \mathit{pblock} } }} :: pblock_ ::=
{{ com programmable block }}
| pbl_type b_func_map t_scope pars_map tbl_map :: :: regular

ffblock {{ tex { \mathit{ffblock} } }} :: ffblock_ ::=
{{ com fixed-function block }}
| ff :: :: ff

%Fence between pblock and pblock_list
embed
{{ hol

}}
grammar

arch_block :: arch_block_ ::=
{{ com architectural block }}
| inp :: :: inp
| pbl x ( e1 , .. , en ) :: :: pbl
| ffbl x :: :: ffbl
| out :: :: out

pblock_list {{ tex \overline{ \mathit{pb} } }} :: pblock_list_ ::=
{{ com list of programmable blocks }}
{{ hol (pblock list) }}

pblock_map {{ tex B_p }} :: pblock_map_ ::=
{{ com programmable block map }}
{{ hol ((string, pblock) alist) }}

ffblock_map {{ tex B_{ \mathrm{ff} } }} :: ffblock_map_ ::=
{{ com fixed-function block map }}
{{ hol ((string, ffblock) alist) }}

in_out_list {{ tex \overline{ \mathit{io} } }} :: in_out_list_ ::=
{{ com list of input and output }}
{{ hol (in_out list) }}

%Fence between in_out_list and input_f
embed
{{ hol

}}
grammar

%The list of architectural blocks describes stages of the pipeline in a specific (package in a specific) architecture
ab_list {{ tex \overline{ \mathit{ab} } }} :: ab_list_ ::=
{{ com list of architectural blocks }}
{{ hol (arch_block list) }}

%The input function modifies the raw input at the start of the pipeline
input_f {{ tex { \mathit{in}_A } }} :: input_f_ ::=
{{ com input function }}
{{ hol ((in_out_list # ascope_ty) -> (in_out_list # ascope_ty) option) }}

%The output function modifies the raw output at the end of the pipeline
output_f {{ tex { \mathit{out}_A } }} :: output_f_ ::=
{{ com output function }}
{{ hol ((in_out_list # ascope_ty) -> (in_out_list # ascope_ty) option) }}

%This function is used to copy things from the architecture when a programmable block begins
copyin_pbl {{ tex { \mathit{in}_p } }} :: copyin_pbl_ ::=
{{ com copy-in to programmable blocks function }}
{{ hol ((x list # d list # e list # ascope_ty) -> scope option) }}

%This function is used to copy out things to the architecture after a programmable block is finished
copyout_pbl {{ tex { \mathit{out}_p } }} :: copyout_pbl_ ::=
{{ com copy-out from programmable blocks function }}
{{ hol ((g_scope list # ascope_ty # d list # x list # status) -> ascope_ty option) }}

%This function takes a table name, arguments, match kinds and the architectural scope and returns an action with arguments
apply_table_f {{ tex { \mathit{apply\_table} } }} :: apply_table_f_ ::=
{{ com apply table function }}
{{ hol ((x # e_list # mk_list # (x # e_list) # ascope_ty) -> (x # e_list) option) }}

%Fence between ab_list and actx
embed
{{ hol

}}
grammar

%The architectural context stores everything that's needed for, but stays invariant over, architecture-level reductions
actx {{ tex { \mathit{ctx}_A } }} :: actx_ ::=
{{ com architectural context }}
{{ hol (ab_list # pblock_map # ffblock_map # input_f # output_f # copyin_pbl # copyout_pbl # apply_table_f # ext_map # func_map) }}
| ( ab_list , pblock_map , ffblock_map , input_f , output_f , copyin_pbl , copyout_pbl , apply_table_f , ext_map , func_map ) :: M :: tup
  {{ com tuple }}
  {{ hol ([[ab_list]], [[pblock_map]], [[ffblock_map]], [[input_f]], [[output_f]], [[copyin_pbl]], [[copyout_pbl]], [[apply_table_f]], [[ext_map]], [[func_map]]) }}

%The context stores everything that's needed for, but stays invariant over, statement reductions
ctx {{ tex \mathit{ctx} }} :: ctx_ ::=
{{ com context }}
{{ hol (apply_table_f # ext_map # func_map # b_func_map # pars_map # tbl_map) }}
| ( apply_table_f , ext_map , func_map , b_func_map , pars_map , tbl_map ) :: M :: tup
  {{ com tuple }}
  {{ hol ([[apply_table_f]], [[ext_map]], [[func_map]], [[b_func_map]] , [[pars_map]], [[tbl_map]]) }}