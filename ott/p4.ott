embed
{{ hol
open wordsTheory;
}}

%%%%%%%%%%%%%%%%%
% P4 base types %
%%%%%%%%%%%%%%%%%

%x denotes variable name, f denotes field name or function name
%msg denotes error message
metavar x, f, msg ::=
      {{ lex alphanum }}
      {{ hol string }}
      {{ tex [[x]] }}
      {{ com string }}
%TODO Numeral or alphanum?
metavar b ::=
      {{ lex numeral }}
      {{ hol bool }}
      {{ tex [[b]] }}
      {{ com boolean }}
%TODO Ideally, one should have different word lengths
metavar nw ::=
      {{ lex numeral }}
      {{ hol word64 }}
%TODO Causes problems with double subscript in LaTeX...
%      {{ tex [[n]]_{64} }}
      {{ tex {n_w} }}
      {{ com integer }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Auxiliary metavariables %
%%%%%%%%%%%%%%%%%%%%%%%%%%%

metavar i ::=
      {{ lex numeral }}
      {{ hol num }}
      {{ tex [[i]] }}
      {{ com natural number }}
indexvar m, n, o ::=
  {{ lex numeral }}
  {{ coq nat }}
  {{ hol num }}
  {{ com indices }}
grammar 

%Statement semantics
%TODO: Add directed arguments (in, out, inout)

%%%%%%%%%%%%%%%
% Expressions %
%%%%%%%%%%%%%%%

%Unary operations
unop {{ tex \ominus }}:: unop_ ::=
| ! :: :: neg_bool
  {{ com boolean negation }}
  {{ tex ! }}
| ~ :: :: compl
  {{ com bitwise complement }}
  {{ tex \neg }}
| - :: :: neg_signed
  {{ com signed negation }}
| + :: :: un_plus
  %Defined as no-op for all values?
  {{ com unary plus }}

%Binary operations
%TODO: Saturating addition?
%TODO: Saturating subtraction?
binop {{ tex \oplus }} :: binop_ ::=
| * :: :: mul
  {{ com multiplication }}
  {{ tex \times }}
| / :: :: div
  {{ com division }}
| mod :: :: mod
  {{ com modulo }}
  {{ tex \bmod }}
  %Denoted by % in P4 specification
| + :: :: add
  {{ com addition }}
| - :: :: sub
  {{ com subtraction }}
| SHL :: :: shl
  {{ com left-shift }}
  {{ tex \ll }}
| SHR :: :: shr
  {{ com right-shift }}
  {{ tex \gg }}
| LE :: :: le
  {{ com less or equal }}
  {{ tex \leq }}
| GE :: :: ge
  {{ com greater or equal }}
  {{ tex \geq }}
| < :: :: ls
  {{ com less }}
  {{ tex < }}
| > :: :: gt
  {{ com greater }}
  {{ tex > }}
| NE :: :: neq
  {{ com not equal }}
  {{ tex \neq }}
| EQ :: :: eq
  {{ com equal }}
  {{ tex = }}
| & :: :: and
  {{ com bitwise and }}
| ^ :: :: xor
  {{ com bitwise xor }}
  {{ tex \underline{\vee} }}
| '|' :: :: or
  {{ com bitwise or }}
  {{ tex \mid }}
| AND :: :: bin_and
  {{ com binary and }}
  {{ tex \land }}
| OR :: :: bin_or
  {{ com binary or }}
  {{ tex \lor }}

bool :: bool_ ::=
{{ com boolean }}
| b :: :: free
%  {{ hol [[b]] }}
| true :: :: true
  {{ hol T }}
| false :: :: false
  {{ hol F }}

%TODO: String literal
%TODO: L-values and identifiers
%TODO: Enums
%Constants should be thought of as "values of variables we look up"
v :: v_ ::=
{{ com constant }}
| bool :: :: bool
  {{ com boolean value }}
| nw :: :: int
  {{ com integer }}
| struct { x1 = v1 ; ... ; xn = vn } :: :: struct
  {{ com struct }}
  {{ tex struct }} %TODO
| header { x1 = v1 ; ... ; xn = vn } :: :: header
  % A header is like a struct, but has different well-formedness conditions
  {{ com header }}
  {{ tex header }} %TODO
%%TODO: An extern should contain an internally visible list of mappings between variable names and constants,
%%      and a map from function names to their bodies and arguments
%% See https://github.com/p4lang/p4c/blob/main/p4include/core.p4 , e.g. packet_in definition
| errmsg x :: :: err
  {{ com error message }}
%TODO: Represent uninitialised variables? Default values?
%See https://p4lang.github.io/p4-spec/docs/P4-16-v1.2.2.html#sec-uninitialized-values-and-writing-invalid-headers
| uninit :: :: uninit
  {{ com uninitialized value }}
  {{ tex ? }}
  {{ hol (ARB) }}

%TODO: Array element
%TODO: Bitslice
lval :: lval_ ::=
| x :: :: varname
  {{ com variable name }}
| lval . f :: :: field
  {{ com field access }}

%TODO: Casts
%TODO: Lists
%TODO: Tuples
%TODO: Conditional operator
exp :: exp_ ::=
{{ com expression }}
| v :: :: v
 {{com constant value}}
| x :: :: var
  {{ com variable name }}
| exp . exp' :: :: acc
  {{ com field access }}
| unop exp :: :: unop
  {{ com unary operation }}
  {{ tex [[unop]] [[exp]] }}
| exp1 binop exp2 :: :: binop
  {{ com binary operation }}
  {{ tex [[exp1]] [[binop]] [[exp2]] }}
| call f ( exp1 , .. , expn ) :: :: func_call
  {{ com function call }}
  {{ hol (exp_func_call [[f]] [[exp1 .. expn]]) }}
| exec stmt :: :: func_exec
  {{ com function execution }}
| ( exp ) :: X :: paren
  {{ hol ([[exp]]) }}

%TODO: Trying to make a new definition for expression lists (mainly for function
%      arguments) proved problematic.
%      If all productions are meta, then you get args as a type abbreviation of "exp",
%      which is either undefined at the point where the abbreviation is made, or
%      exp_list is unknown when defining exp. Either way, mutually recursive definitions
%      are not possible. If nothing is meta and exp_list is not exported to a type
%      abbreviation, the function call semantics will not understand the "exp_list" and "exp list" correspondence.
exp_list :: exp_list_ ::=
{{ com list of expressions }}
{{ hol (exp list) }}
| empty_el :: M :: empty
  {{ com empty expression list }}
  {{ hol ([]:exp list) }}
| exp1 , .. , expn :: M :: exps
  {{ com list of expressions }}
  {{ hol ([[exp1 .. expn]]) }}
| exp_list ++ exp_list' :: M :: append
  {{ com append two exp_lists }}
  {{ hol ([[exp_list]] ++ [[exp_list']]) }}
| update ( exp , i , exp_list ) :: M :: update
  {{ com update entry of expression list }}
  {{ hol (LUPDATE [[exp]] [[i]] [[exp_list]]) }}
  {{ tex ([[i]] \mapsto  [[exp]]) [[exp_list]] }}
| ( exp_list ) :: S :: paren
  {{ hol ([[exp_list]]) }}


%%%%%%%%%%%%%%
% Statements %
%%%%%%%%%%%%%%

%decl :: decl_ ::=
%{{ com declaration }}
%| empty_decl :: :: empty
%  {{ com empty declaration }}
%  {{ tex \emptyset_{ \mathrm{decl} } }}
%| declare x exp :: :: stmt
%  {{ com declaration }}
%  {{ tex [[x]] \mathrel{\mathop:}:= [[exp]] }}
%| decl1 ; decl2 :: :: seq
%  {{ com sequence }}

pars_fin {{ tex p_{ \mathrm{fin} } }} :: pars_fin ::=
{{ com final parser states }}
| Accept :: :: accept
  {{ com accepted state }}
| Reject x :: :: reject
  {{ com rejected state }}

pars_next {{ tex p }} :: pars_next_ ::=
{{ com parser next state }}
| Trans x :: :: trans
  {{ com transition to named state }}
| pars_fin :: :: pars_fin
  {{ com final state }}

%TODO: Switch statement
stmt :: stmt_ ::=
{{ com statement }}
| empty_stmt :: :: empty
  {{ com empty statement }}
  {{ tex \emptyset_{ \mathrm{stmt} } }}
| assign x exp :: :: ass
  {{ com assignment }}
  {{ tex [[x]] \mathrel{\mathop:}= [[exp]] }}
| if exp then stmt1 else stmt2 :: :: cond
  {{ com conditional }}
%TODO: Declaration should take a type as well
| decl x :: :: declare
  {{ com declaration }}
  {{ tex decl \, [[x]] }}
| begin stmt end :: :: block 
  {{ com block }}
  {{ tex \{ [[stmt]] \} }}
| begin' stmt end' :: :: block_ip
  {{ com block in progress }}
  {{ tex [ [[stmt]] ] }}
| return exp :: :: ret
  {{ com return }}
| stmt1 ; stmt2 :: :: seq
  {{ com sequence }}
| verify exp exp' :: :: verify
  % exp is predicate, exp' is error message
  {{ com verify }}
| transition pars_next :: :: trans
  % x is the parser state name
  {{ com transition }}
  {{ tex transition [[pars_next]] }}

func_map {{ tex F }} :: func_map_ ::=
{{ com function map  }}
{{ hol (string |-> (stmt # (string list))) }}
| empty :: M :: empty
  {{ com empty function map }}
  {{ hol FEMPTY }}
| func_map + [ x |-> stmt ] :: M :: update
  {{ com update variable mapping }}
  {{ hol (FUPDATE [[func_map]] ([[x]], [[stmt]])) }}

% TODO: Could be just an abbreviation, since it's never changed...
pars_map {{ tex P }} :: pars_map_ ::=
{{ com parser state map  }}
{{ hol (string |-> stmt) }}
| empty :: M :: empty
  {{ com empty parser state map }}
  {{ hol FEMPTY }}

%%%%%%%%%%%%%%%%%%%
% Execution state %
%%%%%%%%%%%%%%%%%%%
%Note that this is not part of the P4 specification, but made up for verification purposes.

%Execution status.
status {{ tex t }}:: status_ ::=
 {{ com execution status }}
| Running :: :: running
  {{ tex { \mathbf{R} } }}
| Return v :: :: return
  {{ tex { \mathbf{Ret} \,\, [[v]]} }}
| pars_next :: :: pars_next
| TypeError :: :: type_error
  {{ tex { \bot } }}

scope {{ tex \gamma }} :: scope_ ::=
{{ hol (string |-> v) }}
{{ com frame/variable environment of current scope }}
| emptyscope :: M :: empty
  {{ com empty stack frame }}
  {{ hol FEMPTY }}
  {{ tex {\gamma}_{ \emptyset } }}
| scope + [ x |-> v ] :: M :: update
  {{ com update variable mapping }}
  {{ hol (FUPDATE [[scope]] ([[x]], [[v]])) }}
  {{ tex ([[x]] \mapsto  [[v]]) [[scope]] }}
| scope ++ [ x1 |-> v1 , .. , xn |-> vn ] :: M :: update_list
  {{ com update variable mapping }}
  {{ hol (FUPDATE_LIST [[scope]] [[x1 v1 .. xn vn]]) }}
  {{ tex \forall i . (x_i \mapsto  \mathit{v}_i) [[scope]] }}
| ( scope ) :: S :: paren
  {{ hol ([[scope]]) }}
%| G_scope :: M :: global
%{{ com global scope }}
%{{ tex G_s }}
  

%Fence so that ott does not re-order curr_stack_frame
embed
{{ hol

}}
grammar

curr_stack_frame {{ tex \varepsilon }} :: curr_stack_frame_ ::=
{{ hol (scope list) }}
{{ com current stack frame - stack of block scopes }}
| emptycsf :: M :: empty
  {{ hol ([]:scope list) }}
  {{ tex {\emptyset}_{\varepsilon} }}
| [ scope1 , .. , scopen ] :: M :: list
  {{ hol ([[scope1 .. scopen]]) }}
| tl curr_stack_frame :: M :: tl
  {{ hol (TL [[curr_stack_frame]]) }}
  {{ tex \mathrm{tl} ([[curr_stack_frame]]) }}
| scope '::' curr_stack_frame' :: M :: cons
  {{ hol ([[scope]] :: [[curr_stack_frame']]) }}
  {{ tex [[scope]] \mathrel{\mathop:}: [[curr_stack_frame']] }}
| curr_stack_frame ++ curr_stack_frame' :: M :: append
  {{ hol ([[curr_stack_frame]] ++ [[curr_stack_frame']]) }}
  {{ tex [[curr_stack_frame]] \mathrel{\mathop+}+ [[curr_stack_frame']] }}
| update ( scope , i , curr_stack_frame ) :: M :: update
  {{ hol (LUPDATE [[scope]] [[i]] [[curr_stack_frame]]) }}
  {{ tex ([[i]] \mapsto  [[scope]]) [[curr_stack_frame]] }}
| ( curr_stack_frame ) :: S :: paren
  {{ hol ([[curr_stack_frame]]) }}

%Fence so that ott does not re-order curr_stack_frame
embed
{{ hol

}}
grammar

call_stack {{ tex E }} :: call_stack_ ::=
{{ com stack of caller stack frames }}
{{ hol (curr_stack_frame list) }}
| emptycs :: M :: empty
  {{ hol ([]:curr_stack_frame list) }}
  {{ tex {\emptyset}_{E} }}
| [ curr_stack_frame1 , .. , curr_stack_framen ] :: M :: list
  {{ hol ([[curr_stack_frame1 .. curr_stack_framen]]) }}
| curr_stack_frame '::' call_stack :: M :: cons
  {{ hol ([[curr_stack_frame]] :: [[call_stack]]) }}
  {{ tex [[curr_stack_frame]] \mathrel{\mathop:}: [[call_stack]] }}
| ( call_stack ) :: S :: paren
  {{ hol ([[call_stack]]) }}

stacks {{ tex \sigma }} :: stacks_ ::=
 {{ com stacks in execution state }}
| ( curr_stack_frame , call_stack ) :: :: tup

state {{ tex s}} :: state_ ::=
 {{ com execution state }}
| ( stacks , status ) :: :: tup


%%%%%%%%%%%%%%%%%%%%%%%%
% Expression semantics %
%%%%%%%%%%%%%%%%%%%%%%%%

terminals :: terminals_ ::=
%Statement reduction
| -> :: :: rightarrow {{ tex \rightarrow }}
%Expression reduction
| ~> :: :: rightsquigarrow {{ tex \rightsquigarrow }}
%Parser state reduction
| --> :: :: longrightarrow {{ tex \longrightarrow }}

formula :: formula_ ::=
{{ com formulas }}
| judgement :: :: judgement
  {{ com judgement }}
%Expressions
| ! b = b' :: M :: neg_bool
  {{ hol (~[[b]] = [[b']]) }}
| ~ nw = nw' :: M :: compl
  %TODO
  {{ hol (~ [[nw]] = [[nw']]) }}
| - nw = nw' :: M :: neg_signed
  %TODO
  {{ hol (~ [[nw]] = [[nw']]) }}
| + nw = nw' :: M :: un_plus
  %TODO: Read specification...
  {{ hol ([[nw]] = [[nw']]) }}
| nw * nw' = nw'' :: M :: mul
  {{ hol (word_mul [[nw]] [[nw']] = [[nw'']]) }}
| nw / nw' = nw'' :: M :: div
  {{ hol (word_div [[nw]] [[nw']] = [[nw'']]) }}
| nw mod nw' = nw'' :: M :: mod
  {{ hol (word_mod [[nw]] [[nw']] = [[nw'']]) }}
| nw + nw' = nw'' :: M :: add
  {{ hol (word_add [[nw]] [[nw']] = [[nw'']]) }}
| nw - nw' = nw'' :: M :: sub
  {{ hol (word_sub [[nw]] [[nw']] = [[nw'']]) }}
%TODO: EQ for v?
| nw EQ nw' = b :: M :: eq_word
  {{ hol ([[nw]] = [[nw']] <=> [[b]]) }}
| b EQ b' = b'' :: M :: eq_bool
  {{ hol ([[b]] = [[b']] <=> [[b'']]) }}
| nw SHL nw' = nw'' :: M :: shl
  {{ hol (word_lsl_bv [[nw]] [[nw']] = [[nw'']]) }}
  %TODO: Double-check
| nw SHR nw' = nw'' :: M :: shr
  {{ hol (word_asr_bv [[nw]] [[nw']] = [[nw'']]) }}
  %TODO: Double-check
| nw LE nw' = b :: M :: le
  {{ hol ([[nw]] <= [[nw']] <=> [[b]]) }}
| nw GE nw' = b :: M :: ge
  {{ hol ([[nw]] >= [[nw']] <=> [[b]]) }}
| nw < nw' = b :: M :: lt
  {{ hol ([[nw]] < [[nw']] <=> [[b]]) }}
| nw > nw' = b :: M :: gt
  {{ hol ([[nw]] > [[nw']] <=> [[b]]) }}
| nw NE nw' = b :: M :: neq_word
  {{ hol ([[nw]] <> [[nw']] <=> [[b]]) }}
| nw & nw' = nw'' :: M :: and_word
  {{ hol (word_and [[nw]] [[nw']] = [[nw'']]) }}
| nw ^ nw' = nw'' :: M :: xor_word
  {{ hol (word_xor [[nw]] [[nw']] = [[nw'']]) }}
  %TODO: LaTeX XOR
| nw '|' nw' = nw'' :: M :: or_word
  {{ hol (word_or [[nw]] [[nw']] = [[nw'']]) }}
| b AND b' = b'' :: M :: and_bool
  {{ hol ([[b]] /\ [[b']] = [[b'']]) }}
| b OR b' = b'' :: M :: or_bool
  {{ hol ([[b]] \/ [[b']] = [[b'']]) }}

%Type-transforming operations
| curr_stack_frame = hd call_stack :: M :: call_stack_hd
  {{ hol ([[curr_stack_frame]] = HD [[call_stack]]) }}
  {{ tex [[curr_stack_frame]] = \mathrm{hd} [[call_stack]] }}
| scope = curr_stack_frame [ i ] :: M :: curr_stack_frame_index
  {{ hol ([[scope]] = EL [[i]] [[curr_stack_frame]]) }}
| ( stmt , x1 , .. , xn ) = func_map ( f ) :: M :: func_lookup
  {{ com function lookup }}
  {{ hol (FLOOKUP [[func_map]] [[f]] = SOME ([[stmt]], [[x1 .. xn]])) }}
| find_index_max ( curr_stack_frame , x ) = i :: M :: find_index_scope_max
  {{ com find max index of variable's scope }}
  %INDEX_FIND finds the first occurrence, starting from head and going backwards in list
  {{ hol (FST (THE (INDEX_FIND 0 (\sf. IS_SOME (FLOOKUP sf [[x]])) [[curr_stack_frame]])) = [[i]]) }}
  {{ tex i = max  \{ j . [[x]]  \in \mathrm{dom} ( [[curr_stack_frame]] [j] )   \} }}
%  {{ tex [[x]] \in \mathrm{dom}([[curr_stack_frame]][ [[i]] ]) \land \forall j . \,\, j > [[i]] \Rightarrow [[x]] \notin \mathrm{dom}([[curr_stack_frame]][j]) }}
| length ( curr_stack_frame ) = i :: M :: find_length
  {{ com find the current stack frame length }}
  {{ tex [[i]] = length ([[curr_stack_frame]])}}
  {{ hol ([[i]] = LENGTH [[curr_stack_frame]]) }}
| v = scope ( x ) :: M :: lookup
  {{ com look-up of variable in scope }}
  {{ hol (FLOOKUP [[scope]] [[x]] = SOME [[v]]) }}
| v = struct { x1 = v1 ; ... ; xn = vn } ( x ) :: M :: access
  {{ com access fo field in struct }}
  {{ hol (FIND (\(k, v). k = [[x]]) [[x1 v1 ... xn vn]] = SOME ([[x]], [[v]])) }}
| exp = exp_list [ i ] :: M :: args_index
  {{ hol ([[exp]] = EL [[i]] [[exp_list]]) }}
| exp = hd exp_list :: M :: args_hd
  {{ hol ([[exp]] = HD [[exp_list]]) }}
  {{ tex [[exp]] = \mathrm{hd} \,\, [[exp_list]] }}

%Function argument resolution
%TODO: Replace with expression list
%| v_list ++ exp_list' = exp_list :: M :: func_args_append
%  {{ com pattern match on constant and non-constant function arguments }}
%  {{ hol (([[v_list]] ++ [[exp_list']]) = [[exp_list]]) }}

%Conditions
%TODO: Remove, with new separate "bool" definition?
| b = true :: M :: is_true
  {{ com condition true }}
  {{ hol ([[b]] = T) }}
  {{ tex [[b]] = \mathrm{True} }}
| b = false :: M :: is_false
  {{ com condition false }}
  {{ hol ([[b]] = F) }}
  {{ tex [[b]] = \mathrm{False} }}
| is_empty ( exp_list ) :: M :: args_empty
  {{ com function arguments empty }}
  {{ hol (NULL [[exp_list]]) }}
  {{ tex \mathit{exp}_1 , .. , \mathit{exp}_n = \{ \} }}

%Equalities
| scope = scope' :: M :: scope_eq
  {{ com scope equality }}
  {{ hol ([[scope]] = [[scope']]) }}
| curr_stack_frame = curr_stack_frame' :: M :: curr_stack_frame_eq
  {{ com curr_stack_frame equality }}
  {{ hol ([[curr_stack_frame]] = [[curr_stack_frame']]) }}
| call_stack = call_stack' :: M :: call_stack_eq
  {{ com call_stack equality }}
  {{ hol ([[call_stack]] = [[call_stack']]) }}
%TODO: This can cause confusion with the judgements with binary expression equalities, hence the '...
| exp_list =' exp_list' :: M :: args_eq
  {{ com args equality }}
  {{ hol ([[exp_list]] = [[exp_list']]) }}
  {{ tex [[exp_list]] = [[exp_list']] }}

%MISC (TODO)
%TODO: Equalities and inequalities on numerals
| i' = i + 1 :: M :: num_inc
  {{ com numeral increment }}
  {{ hol ([[i']] = [[i]] + 1) }}
  {{ tex [[i']] = [[i]] + 1 }}
| i = length ( exp_list ) :: M :: num_len
  {{ com length of list }}
  {{ hol ([[i]] = LENGTH [[exp_list]]) }}
  {{ tex [[i]] = \mathrm{len} \,\, [[exp_list]] }}
| i < length ( exp_list ) :: M :: num_in_rg
  {{ com numeral in range }}
  {{ hol ([[i]] < LENGTH [[exp_list]]) }}
  {{ tex [[i]] < \mathrm{len}(\mathit{exp}) }}
| is_consts ( exp_list ) :: M :: args_const
  {{ com function arguments constant }}
  {{ hol (is_consts [[exp_list]]) }}
  %TODO: Fix typesetting
  {{ tex \mathrm{is\_consts} \,\, [[exp_list]] }}
| stmt = pars_map ( x ) :: M :: pars_lookup
  {{ com parser state map lookup }}
  {{ hol (FLOOKUP [[pars_map]] [[x]] = SOME ([[stmt]])) }}
| x = empty_err_msg :: M :: empty_err_msg
  {{ com empty error message }}
  {{ hol ([[x]] = "ParserStateEnd") }}
  {{ tex [[x]] = \mathrm{ParserStateEnd} }}

%TODO: Review if this is needed...
embed
{{ hol
val is_const_def = Define `
    ( is_const (exp_const _) = T)
/\  ( is_const _ = F)
`;
val is_consts_def = Define `
    (is_consts exp_list = ~(EXISTS (\el. ~(is_const el)) exp_list))
`;
}}
grammar

defns
  exp_sem :: '' ::=
defn
  [ exp ] ( stacks , status ) ~> [ exp' ] ( stacks' , status' ) :: :: exp_red :: exp_
  {{ com expression semantics }}
  %Uses different export homs for LaTeX and HOL4 so that LaTeX hides unnecessary status
  {{ tex [ [[exp]] ] [[stacks]] \rightsquigarrow [ [[exp']] ] [[stacks']] }}
by

%%%%%%%%%%%%%%
%Function call + exec (in expression)

  %Resolving arguments to function call - assigning arguments to new stack frame
  ( stmt , x1 , .. , xn ) = func_map ( f )
  scope' = emptyscope ++ [ x1 |->  v1 , .. , xn |-> vn ]
%TODO: Global scope notation
%  G_scope = curr_stack_frame[0]
%  curr_stack_frame' = scope' :: [G_scope]
  scope = curr_stack_frame[0]
  curr_stack_frame' = scope' :: [scope]
  call_stack' = tl curr_stack_frame :: call_stack
  ----------------------------------- :: func_call_newframe
  [call f ( v1 , .. , vn )] ( ( curr_stack_frame , call_stack ) , Running ) ~> [ exec stmt ] ( ( curr_stack_frame' , call_stack' ) , Running )

  [stmt] ( stacks , Running ) -> [stmt'] ( stacks' , Running )
  ----------------------------------- :: func_exec
  [exec stmt] ( stacks , Running ) ~> [exec stmt'] ( stacks' , Running )

%TODO: Note that this is a simplification of the return mechanism
  ----------------------------------- :: func_ret_exp
  [exec empty_stmt] ( stacks , Return v ) ~> [v] ( stacks' , Running )

%%%%%%%%%%%%%%%%%
%Variable look-up

  find_index_max ( curr_stack_frame, x ) = i
  scope = curr_stack_frame [ i ]
  v = scope ( x )
  ----------------------------------- :: lookup
  [x] ( ( curr_stack_frame, call_stack ) , Running ) ~> [v] ( ( curr_stack_frame, call_stack ) , Running )

%%%%%%%%%%%%%%%%%%%%
%Struct/extern field/method access
%TODO: Nested structs

  v = struct { x1 = v1 ; ... ; xn = vn } ( x )
  ----------------------------------- :: acc
  [ struct { x1 = v1 ; ... ; xn = vn } . x] ( stacks , Running ) ~> [v] ( stacks' , Running )

%%%%%%%%%%%%%
%"Reduction of argument" rules

%Resolving arguments to function call - reduction
  %TODO: force exp_list'' to be non-empty?
  exp_list' ++ exp_list'' =' exp1 , .. , expn
  is_consts ( exp_list' )
  exp = hd exp_list''
  [ exp ] ( stacks , Running ) ~> [ exp' ] ( stacks' , Running )
  %TODO: length plus 1 if indexing from 1
  i = length ( exp_list' )
  exp'1 , .. , exp'm =' update ( exp' , i , ( exp1 , .. , expn ) )
  ----------------------------------- :: func_call_args
  [call f ( exp1 , .. , expn )] ( stacks , Running ) ~> [call f ( exp'1 , .. , exp'm )] ( stacks , Running )

%Stacks
  [exp'] ( stacks , Running ) ~> [exp''] ( stacks' , Running )
  ----------------------------------- :: acc_arg2
  [exp . exp'] ( stacks , Running ) ~> [exp . exp''] ( stacks' , Running )

  [exp] ( stacks , Running ) ~> [exp'] ( stacks' , Running )
  ----------------------------------- :: acc_arg1
  [exp . x] ( stacks , Running ) ~> [exp' . x] ( stacks' , Running )

%Unary operations
  [exp] ( stacks , Running ) ~> [exp'] ( stacks' , Running )
  ----------------------------------- :: unop_arg
  [unop exp] ( stacks , Running ) ~> [unop exp'] ( stacks' , Running )

%Binary operations
  [exp] ( stacks , Running ) ~> [exp''] ( stacks' , Running )
  ----------------------------------- :: binop_arg1
  [exp binop exp'] ( stacks , Running ) ~> [exp'' binop exp'] ( stacks' , Running )

  [exp'] ( stacks , Running ) ~> [exp''] ( stacks' , Running )
  ----------------------------------- :: binop_arg2
  [v binop exp'] ( stacks , Running ) ~> [exp binop exp''] ( stacks' , Running )


%%%%%%%%%%%%%%%%%%%
%Unary expressions%
%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%
%Boolean negation

  ! b = b'
  ----------------------------------- :: neg_bool
  [! b] ( stacks , Running ) ~> [b'] ( stacks , Running )

%%%%%%%%%%%%%%%%%%%
%Bitwise complement

  ~ nw = nw'
  ----------------------------------- :: compl
  [~ nw] ( stacks , Running ) ~> [nw'] ( stacks , Running )

%%%%%%%%%%%%%%%%
%Signed negation

  - nw = nw'
  ----------------------------------- :: neg_signed
  [- nw] ( stacks , Running ) ~> [nw'] ( stacks , Running )

%%%%%%%%%%%
%Unary plus

  + nw = nw'
  ----------------------------------- :: un_plus
  [+ nw] ( stacks , Running ) ~> [nw'] ( stacks , Running )


%%%%%%%%%%%%%%%%%%%%
%Binary expressions%
%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%
%Multiplication

  nw * nw' = nw''
  ----------------------------------- :: mul
  [nw * nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%%%%
%Division

  nw / nw' = nw''
  ----------------------------------- :: div
  [nw / nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%%%%
%Modulo

  nw mod nw' = nw''
  ----------------------------------- :: mod
  [nw mod nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%
%Addition

  nw + nw' = nw''
  ----------------------------------- :: add
  [nw + nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%
%Subtraction

  nw - nw' = nw''
  ----------------------------------- :: sub
  [nw - nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%
%Left shift

  nw SHL nw' = nw''
  ----------------------------------- :: shl
  [nw SHL nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%
%Right shift

  nw SHR nw' = nw''
  ----------------------------------- :: shr
  [nw SHR nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%%%%%%%%
%Less than or equal

  nw LE nw' = b
  ----------------------------------- :: le
  [nw LE nw'] ( stacks , Running ) ~> [b] ( stacks , Running )

%%%%%%%%%%%%%%%%%%%%%%
%Greater than or equal

  nw GE nw' = b
  ----------------------------------- :: ge
  [nw GE nw'] ( stacks , Running ) ~> [b] ( stacks , Running )

%%%%%%%%%%
%Less than

  nw < nw' = b
  ----------------------------------- :: lt
  [nw < nw'] ( stacks , Running ) ~> [b] ( stacks , Running )

%%%%%%%%%%%%%
%Greater than

  nw > nw' = b
  ----------------------------------- :: gt
  [nw > nw'] ( stacks , Running ) ~> [b] ( stacks , Running )

%%%%%%%%%%%%%
%Not equal

  nw NE nw' = b
  ----------------------------------- :: neq
  [nw NE nw'] ( stacks , Running ) ~> [b] ( stacks , Running )

%%%%%%%%%%%%%
%Equal

  nw EQ nw' = b
  ----------------------------------- :: eq
  [nw EQ nw'] ( stacks , Running ) ~> [b] ( stacks , Running )

%%%%%%%%%%%%%
%Bitwise and

  nw & nw' = nw''
  ----------------------------------- :: and
  [nw & nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%%
%Bitwise xor

  nw ^ nw' = nw''
  ----------------------------------- :: xor
  [nw ^ nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%%
%Bitwise or

  nw | nw' = nw''
  ----------------------------------- :: or
  [nw | nw'] ( stacks , Running ) ~> [nw''] ( stacks , Running )

%%%%%%%%%%%%%
%Binary and

  b AND b' = b''
  ----------------------------------- :: bin_and
  [b AND b'] ( stacks , Running ) ~> [b''] ( stacks , Running )

%%%%%%%%%%%%%
%Binary or

  b OR b' = b''
  ----------------------------------- :: bin_or
  [b OR b'] ( stacks , Running ) ~> [b''] ( stacks , Running )


%%%%%%%%%%%%%%%%%%%%%%%%%
% Declaration semantics %
%%%%%%%%%%%%%%%%%%%%%%%%%

%defns
%  decl_sem :: '' ::=
%defn
%  [ decl ] state -> [ decl' ] state' :: :: decl_red :: decl_
%  {{ com declaration semantics }}
%by

  %Declare a variable and assign a value - reduction step
%  [exp] ( stacks , Running ) ~> [exp'] ( stacks' , Running )
%  ----------------------------------- :: decl_exp
%  [declare x exp] ( stacks , Running ) -> [declare x exp'] ( stacks' , Running )

%  scope :: curr_stack_frame' = curr_stack_frame
%  scope' = scope + [ x |-> n ]
%  curr_stack_frame'' = scope' :: curr_stack_frame'
%  ----------------------------------- :: decl_const
%  [declare x n] ( ( curr_stack_frame , call_stack ) , Running ) -> [empty_decl] ( ( curr_stack_frame'' , call_stack ) , Running )

 % [decl1] ( stacks , Running ) -> [decl1'] ( stacks' , Running )
 % ----------------------------------- :: seq1
 % [decl1 ; decl2] ( stacks , Running ) -> [decl1' ; decl2] ( stacks' , Running )

 % ----------------------------------- :: seq2
 % [empty_decl ; decl] ( stacks , Running ) -> [decl] ( stacks , Running )


%%%%%%%%%%%%%%%%%%%%%%%
% Statement semantics %
%%%%%%%%%%%%%%%%%%%%%%%

%TODO: Block
%TODO: Exit
%TODO: Switch

defns
  stmt_sem :: '' ::=
defn
  [ stmt ] state -> [ stmt' ] state' :: :: stmt_red :: stmt_
  {{ com statement semantics }}
by

%%%%%%%%%%%%%%
%Return

%TODO: Global scope
%  G_scope = curr_stack_frame [ 0 ]
  scope = curr_stack_frame [ 0 ]
  curr_stack_frame' :: call_stack' = call_stack
  curr_stack_frame'' = ( curr_stack_frame' ) ++ ( [ scope ] )
  ----------------------------------- :: ret
  [return v] ( ( curr_stack_frame , call_stack ) , Running ) -> [empty_stmt] ( ( curr_stack_frame'' , call_stack' ) , Return v )

%%%%%%%%%%%%%%
%Assignment

  %Assign a constant to a variable
  find_index_max ( curr_stack_frame , x ) = i
  scope = curr_stack_frame [ i ]
  curr_stack_frame' = update ( scope + [ x |-> v ] , i , curr_stack_frame )
  ----------------------------------- :: ass_v
  [assign x v] ( ( curr_stack_frame , call_stack ) , Running ) -> [empty_stmt] ( ( curr_stack_frame' , call_stack ) , Running )

  %TODO: Assign to a struct field

%%%%%%%%%%%%%%
%Sequence

  [stmt1] ( stacks , Running ) -> [stmt1'] ( stacks' , Running )
  ----------------------------------- :: seq1
  [stmt1 ; stmt2] ( stacks , Running ) -> [stmt1' ; stmt2] ( stacks' , Running )

  ----------------------------------- :: seq2
  [empty_stmt ; stmt] ( stacks , Running ) -> [stmt] ( stacks , Running )

  [stmt1] ( stacks , Running ) -> [stmt1'] ( stacks' , Return v )
  ----------------------------------- :: seq3
  [stmt1 ; stmt2] ( stacks , Running ) -> [ empty_stmt ] ( stacks' , Return v )

%%%%%%%%%%%%%%
%Conditional

  ----------------------------------- :: cond2
  [if true then stmt1 else stmt2] ( stacks , Running ) -> [stmt1] ( stacks , Running )

  ----------------------------------- :: cond3
  [if false then stmt1 else stmt2] ( stacks , Running ) -> [stmt2] ( stacks , Running )

%%%%%%%%%%%%%%
%declaration

  length ( curr_stack_frame ) = i
  scope = curr_stack_frame [ i ]
  curr_stack_frame' = update ( scope + [ x |-> uninit ] , i , curr_stack_frame )
  ----------------------------------- :: decl
  [decl x] ( ( curr_stack_frame , call_stack ) , Running ) -> [empty_stmt] ( ( curr_stack_frame' , call_stack ) , Running )


%%%%%%%%%%%%%%
%Block

  curr_stack_frame' = emptyscope :: curr_stack_frame
  ----------------------------------- :: block_enter
  [begin stmt end] ( ( curr_stack_frame , call_stack ) , Running ) -> [begin' stmt end'] ( ( curr_stack_frame' , call_stack ) , Running )

  [stmt] ( stacks , Running ) -> [stmt'] ( stacks' , Running )
  ----------------------------------- :: block_exec
  [begin' stmt end'] ( stacks , Running ) -> [begin' stmt' end'] ( stacks' , Running )

  curr_stack_frame' = tl curr_stack_frame
  ----------------------------------- :: block_exit
  [begin' empty_stmt end'] ( ( curr_stack_frame , call_stack ) , Running ) -> [empty_stmt] ( ( curr_stack_frame' , call_stack ) , Running )

%%%%%%%%%%%%%%
%Verify

%Case predicate holds
  ------------------------------------------------- :: verify_3
  [verify true ( errmsg x )]( stacks , Running ) -> [empty_stmt]( stacks' , Running )

%Case predicate does not hold
  ------------------------------------------------- :: verify_4
  [verify false ( errmsg x )]( stacks , Running ) -> [empty_stmt]( stacks' , Reject x )

%%%%%%%%%%%%%%
%Transition

  ------------------------------------------------- :: transition
  [transition pars_next]( stacks , Running ) -> [empty_stmt]( stacks , pars_next )

%Transition to named state
%NOTE: This assumes transition is last statement
%TODO: Requires statement to be found in pars_map
%  ------------------------------------------------- :: transition_1
%  [transition x]( stacks , Running ) -> [empty_stmt]( stacks , Trans x )

%Transition to accept
%NOTE: This assumes transition is last statement
%  ------------------------------------------------- :: transition_2
%  [transition accept]( stacks , Running ) -> [empty_stmt]( stacks , Accept )

%Transition to reject
%NOTE: This assumes transition is last statement
%  ------------------------------------------------- :: transition_3
%  [transition reject x]( stacks , Running ) -> [empty_stmt]( stacks , Reject x )


%%%%%%%%%%%%%%
%Exit

  %TODO: exit semantics might require to have a specific "function-in-progress" statement
  %      in order to know what to reduce to nothing.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Reduction of expression rules %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%Reduction step of return expression
  [exp] ( stacks , Running ) ~> [exp'] ( stacks' , Running )
  ----------------------------------- :: ret_exp
  [return exp] ( stacks , Running ) -> [return exp'] ( stacks' , Running )

%Reduction step of assign expression
  [exp] ( stacks , Running ) ~> [exp'] ( stacks' , Running )
  ----------------------------------- :: ass_exp
  [assign x exp] ( stacks , Running ) -> [assign x exp'] ( stacks' , Running )

%Reduction step of condition
  [exp] ( stacks , Running ) ~> [exp'] ( stacks' , Running )
  ----------------------------------- :: cond_exp
  [if exp then stmt1 else stmt2] ( stacks , Running ) -> [if exp' then stmt1 else stmt2] ( stacks' , Running )

%Reduction step of verify predicate
  [exp] ( stacks , Running ) ~> [exp''] ( stacks' , Running )
  ------------------------------------------------- :: verify_exp1
  [verify exp exp' ]( stacks , Running ) -> [verify exp'' exp']( stacks' , Running )

%Reduction step of error message
  [exp] ( stacks , Running ) ~> [exp'] ( stacks' , Running )
  ------------------------------------------------- :: verify_exp2
  [verify b exp]( stacks , Running ) -> [verify b exp']( stacks' , Running )


%%%%%%%%%%%%%%%%%%%%
% Parser semantics %
%%%%%%%%%%%%%%%%%%%%
%This isolates the unstructured parser semantics into a set of rules defined in terms
%of the regular statement semantics.

defns
  pars_sem :: '' ::=
defn
  [ stmt ] state --> [ stmt' ] state' :: :: pars_red :: pars_
  {{ com parser semantics }}
by

  [stmt]( stacks , Running ) -> [stmt']( stacks' , Running )
  ------------------------------------------------- :: stmt
  [stmt]( stacks , Running ) --> [stmt']( stacks' , Running )

  [stmt]( stacks , Running ) -> [stmt']( stacks' , Trans x )
  stmt'' = pars_map ( x )
  ------------------------------------------------- :: state
  [stmt]( stacks , Running ) --> [stmt'']( stacks' , Running )


defns
  pars_t_sem :: '' ::=
defn
  [ stmt ] state --> state' :: :: pars_t_red :: pars_t_
  {{ com parser semantics, final step }}
by

  [stmt]( stacks , Running ) -> [stmt']( stacks , pars_fin )
  ------------------------------------------------- :: fin
  [stmt]( stacks , Running ) --> ( stacks , pars_fin )

%If the end of some parser state is reached without status change, we reject
  [stmt]( stacks , Running ) -> [empty_stmt]( stacks' , Running )
  x = empty_err_msg
  ------------------------------------------------- :: empty
  [stmt]( stacks , Running ) --> ( stacks' , Reject x )