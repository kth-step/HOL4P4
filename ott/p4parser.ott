metavar f ::=
 {{ lex alphanum }}
 {{ hol string }}
 {{ com field }}
metavar m ::=
 {{ lex alphanum }}
 {{ hol string }}
 {{ com message }}
metavar out ::=
 {{ lex alphanum }}
 {{ hol string |-> bool list }}
 {{ com output }}
indexvar k, n ::=
 {{ lex numeral }}
 {{ hol num }}
 {{ com index variable }}
grammar
bv, inp :: bv_ ::=
 {{ hol bool list }}
 {{ com bit vector }}
| [ b1 , .. , bn ] :: M :: enum
  {{ hol [[b1 .. bn]] }}

b :: b_ ::=
{{ com boolean }}
{{ hol bool }}
| bv = bv' :: M :: bv_eq
  {{ hol ([[bv]] = [[bv']]) }}
| true :: M :: true
  {{ hol T }}
| false :: M :: false
  {{ hol F }}

exp :: exp_ ::=
 {{ com expression }}
| In :: :: input
| Out . f :: :: output
| exp == exp' :: :: eq

stm :: stm_ ::=
 {{ com statement }}
| extract f :: :: extract

status :: status_ ::=
 {{ com execution status }}
| Running :: :: running
| TypeError :: :: type_error
| Reject m :: :: reject
| JumpOutside :: :: jump_outside

state :: state_ ::=
 {{ com execution state }}
| ( inp , out , status ) :: :: ct

terminals :: terminals_ ::=
| -> :: :: rightarrow {{ tex \rightarrow }}

formula :: formula_ ::=
{{ com formulas }}
| judgement :: :: judgement
  {{ com judgement }}
| out . f = bv :: M :: out_f_eq_bv
  {{ hol (FLOOKUP [[out]] [[f]] = SOME [[bv]]) }}
| k < n :: M :: lt
  {{ hol ([[k]] < [[n]]) }}
| bv = bv' :: M :: bv_eq
  {{ hol ([[bv]] = [[bv']]) }}
| size ( f ) = n :: M :: size_f
  {{ hol (0 = [[n]]) }}

defns
  p4_parser_exp_red :: '' ::=
defn
  [ exp ] state -> b :: :: exp_red :: exp_red_
  {{ com expression semantics }}
by

  out.f = bv  
  ----------------------------------- :: eq
  [In == Out.f](inp,out,status) -> inp = bv

defns
  p4_parser_stm_red :: '' ::=
defn
  [ stm ] state -> state' :: :: stm_red :: stm_red_
  {{ com statement semantics }}
by

  size(f) = k
  inp = [b1,..,bk,b'1,..,b'n]
  inp' = [b'1,..,b'n]
  out'.f = [b1,..,bk]
  ------------------------------------------------- :: extract_1
  [extract f](inp,out,status) -> (inp',out',status)

  size(f) = k
  inp = [b1,..,bn]
  n < k
  -------------------------------------------------- :: extract_2
  [extract f](inp,out,status) -> (inp,out,TypeError)
